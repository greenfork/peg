digit19 <- '1'..'9'
digit <- '0'..'9'
digits <- digit+
int <- '-' digit19 digits / '-' digit / digit19 digits / digit
frac <- '.' digits
exp <- ('e' / 'E') ('-' / '+')? digits
Number <- int frac? exp?

hex <- digit / 'a'..'f' / 'A'..'F'
char <-
  "\\\"" / "\\\\" / "\\/" / "\\b" / "\\f" / "\\n" / "\\r" / "\\t" /
  "\\u" hex hex hex hex / !"\"" !"\\" .
String <- "\"" char* "\""
Bool <- "true" / "false"

value <- "null" / Bool / Number / String / object / array
pair <- String -":" value

# TODO: separator nodes to get AST shape right
# TODO: labels
object <- -"{" (pair (-"," pair)*)? -"}"
array <- -"[" (value (-"," value)*)? -"]"

whitespace <- (" " / "\t" / "\r" / "\n")+
linecomment <- "//" (!"\r" !"\n" .)*
nestedcomment <- "/*" ((!"/*" !"*/" .) / nestedcomment)* "*/"

start <- value
hidden <- (whitespace / linecomment / nestedcomment)+
